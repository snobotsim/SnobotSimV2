repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://frcmaven.wpi.edu/artifactory/release/" }
    maven { url 'https://devsite.ctr-electronics.com/maven/release/' }
    maven { url 'https://dev.studica.com/maven/release/2025/' }
    maven { url 'https://maven.revrobotics.com/' }
}


sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

ext.getCurrentArch = {
    OperatingSystem os = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem;

    String arch = System.getProperty("os.arch");
    println(arch)

    if (os.isMacOsX()) {
        return "universal"
    }
    if (arch.equals("amd64") || arch.equals("x86_64")) {
        return "x86-64"
    }
    return "x86"
}

ext.getCurrentOs = {
    OperatingSystem os = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem;
    println(os)
    if (os.isWindows()) {
        return "windows"
    }
    else if (os.isMacOsX()) {
        return "osx"
    }
    else {
        return "linux";
    }
}

ext {
    wpiVersion = '2025.3.2'
    opencvYear = '2025'
    opencvVersion = '4.10.0-3'
    phoenix5Version = '5.35.1'
    phoenix6Version = "25.4.0"
    revVersion = '2025.0.3'
    navxVersion = '2025.0.0'

    nativeOs = getCurrentOs() + getCurrentArch()
}

wpilibTools.deps.wpilibVersion = project.wpiVersion

configurations {
    nativeDesktopZip
}

dependencies {

    nativeDesktopZip "edu.wpi.first.hal:hal-cpp:${wpiVersion}:${nativeOs}@zip"
    nativeDesktopZip "edu.wpi.first.ntcore:ntcore-cpp:${wpiVersion}:${nativeOs}@zip"
    nativeDesktopZip "edu.wpi.first.wpinet:wpinet-cpp:${wpiVersion}:${nativeOs}@zip"
    nativeDesktopZip "edu.wpi.first.cscore:cscore-cpp:${wpiVersion}:${nativeOs}@zip"
    nativeDesktopZip "edu.wpi.first.wpiutil:wpiutil-cpp:${wpiVersion}:${nativeOs}@zip"
    nativeDesktopZip "edu.wpi.first.wpimath:wpimath-cpp:${wpiVersion}:${nativeOs}@zip"
    nativeDesktopZip "edu.wpi.first.thirdparty.frc${opencvYear}.opencv:opencv-cpp:${opencvVersion}:${nativeOs}@zip"

    nativeDesktopZip "com.studica.frc:Studica-driver:${navxVersion}:${nativeOs}@zip"

    nativeDesktopZip "com.ctre.phoenix.sim:cci-sim:${phoenix5Version}:${nativeOs}@zip"

    nativeDesktopZip "com.ctre.phoenix6.sim:api-cpp-sim:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:simCANCoder:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:simPigeonIMU:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:simProCANdi:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:simProCANrange:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:simProCANdle:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:simProCANcoder:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:simProPigeon2:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:simProTalonFX:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:simProTalonFXS:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:simTalonSRX:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:simVictorSPX:${phoenix6Version}:${nativeOs}@zip"
    nativeDesktopZip "com.ctre.phoenix6.sim:tools-sim:${phoenix6Version}:${nativeOs}@zip"

    nativeDesktopZip "com.revrobotics.frc:REVLib-driver:${revVersion}:${nativeOs}@zip"
}

tasks.withType(JavaCompile) {
    options.deprecation = true
}

task extractJniLibraries() {
    def nativeZips = project.configurations.getByName('nativeDesktopZip')
    FileCollection extractedFiles = null as FileCollection

    nativeZips.dependencies
            .matching { Dependency dep -> dep != null && nativeZips.files(dep).size() > 0 }
            .all { Dependency dep ->
                def ziptree = project.zipTree(nativeZips.files(dep).first())
                [
                        "**/*.so*",
                        "**/*.so",
                        "**/*.dll",
                        "**/*.dylib"
                ].collect { String pattern ->
                    def fc = ziptree.matching { PatternFilterable pat -> pat.include(pattern) }
                    if (extractedFiles == null) extractedFiles = fc
                    else extractedFiles += fc
                }
            }

    File dir = new File(project.buildDir, "snobotSimFiles")

    if (!dir.exists()) {
        dir.parentFile.mkdirs()
    }

    def tree = fileTree("${dir}")
    tree.include '**/*.so*'
    tree.include '**/*.so'
    tree.include '**/*.dll'
    tree.include '**/*.dylib'
    tree.each { it.delete() }

    if (extractedFiles != null) {
        project.copy { CopySpec s ->
            s.from(project.files { extractedFiles.files })
            s.into(dir)
        }
    }
}


apply from: "${rootDir}/styleguide/styleguide.gradle"

test {
    systemProperty "java.library.path", "${projectDir}/build/snobotSimFiles"
    environment 'LD_LIBRARY_PATH', "${projectDir}/build/snobotSimFiles"
    workingDir "${projectDir}/build/snobotSimFiles"
    dependsOn(extractJniLibraries)

    useJUnitPlatform {
        // TODO ctre exclusion is temporary while they fix up their beta
        excludeTags "flaky", "ctre"

    }
    testLogging {
        events "failed", "STANDARD_OUT"
        exceptionFormat "full"
    }
    finalizedBy jacocoTestReport
}
