
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'com.github.spotbugs'

jacoco {
    toolVersion = "0.8.8"
}
jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled true
    }
}


checkstyle {
    toolVersion = "10.1"
    configDirectory = file("${project.rootDir}/styleguide")
    config = resources.text.fromFile(new File(configDirectory.get().getAsFile(), "checkstyle.xml"))
}

pmd {
    toolVersion = '6.39.0'
    consoleOutput  = true
    reportsDir = file("${project.buildDir}/reports/pmd")
    ruleSetFiles = files(file("$rootDir/styleguide/pmd-ruleset.xml"))
    ruleSets = []
}

spotbugs {
    toolVersion = '4.7.3'
    excludeFilter = file("$rootDir/styleguide/findbugs_suppressions.xml")
    effort = "max"
}

spotbugsMain {
    reports {
        html {
            enabled = true
        }
    }
}
spotbugsTest {
    reports {
        html {
            enabled = true
        }
    }
}


apply plugin: 'com.diffplug.spotless'

spotless {
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**', 'bazel-*/**'
        }
        toggleOffOn()
        indentWithSpaces(4)
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    groovyGradle {
        target fileTree('.') {
            include '**/*.gradle'
            exclude '**/build/**', '**/build-*/**', 'bazel-*/**'
        }
        greclipse()
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
    format 'xml', {
        target fileTree('.') {
            include '**/*.xml'
            include '**/*.fxml'
            exclude '**/build/**', '**/build-*/**', 'bazel-*/**', '.idea/**'
        }
        eclipseWtp('xml')
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
    format 'misc', {
        target fileTree('.') {
            include '**/*.md', '**/.gitignore', "**/*.yml", "**/*.yaml"
            exclude '**/build/**', '**/build-*/**', 'bazel-*/**'
        }
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
}
